apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

ext {
    versionMajor = 1
    versionMinor = 6
    versionPatch = 8
    versionBuild = 0
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "net.theluckycoder.scriptcraft"
        minSdkVersion 16
        targetSdkVersion 26
        versionName "$versionMajor.$versionMinor.$versionPatch"
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        resConfigs "en", "fr", "ro", "ru"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            resValue "bool", "debug_build", "true"
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            versionNameSuffix ".$versionBuild"
        }
        release {
            resValue "bool", "debug_build", "false"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

kotlin.experimental.coroutines "enable"

ktlint {
    version = "0.21.0"
    verbose = true
    android = true
    ignoreFailures = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5'
    implementation "org.jetbrains.anko:anko-commons:0.10.4"

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'

    implementation 'com.google.firebase:firebase-core:15.0.0'
    implementation 'com.google.firebase:firebase-config:15.0.0'
    implementation 'com.google.firebase:firebase-ads:15.0.0'

    implementation 'net.theluckycoder.materialchooser:materialchooser:1.1.5'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.1.0'
    implementation 'com.takisoft.fix:preference-v7:27.1.1.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
