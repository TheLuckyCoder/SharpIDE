apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

ext {
    versionMajor = 1
    versionMinor = 7
    versionPatch = 1
    versionBuild = 0
}

android {
    compileSdkVersion Versions.compile_sdk
    buildToolsVersion Versions.build_tools

    defaultConfig {
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionName "$versionMajor.$versionMinor.$versionPatch.$versionBuild"
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        resConfigs "en", "fr", "ro", "ru"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            resValue "bool", "debug_build", "true"
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            aaptOptions.cruncherEnabled = false
            versionNameSuffix "-debug"
        }
        release {
            resValue "bool", "debug_build", "false"
            minifyEnabled true
            shrinkResources true
        }
    }

    flavorDimensions "type"
    productFlavors {
        free {
            dimension "type"
            applicationId "net.theluckycoder.scriptcraft"
            proguardFile 'proguard-rules-free.pro'

            resValue "string", "app_name", "SharpIDE"
            resValue "string", "banner_ad_unit_id", "ca-app-pub-1279472163660969/8935487535"
        }
        pro {
            dimension "type"
            applicationId "net.theluckycoder.sharpidepro"
            proguardFile 'proguard-rules-pro.pro'

            resValue "string", "app_name", "SharpIDE Pro"
        }
    }
}

kotlin.experimental.coroutines "enable"

ktlint {
    version = "0.27.0"
    verbose = true
    android = true
    ignoreFailures = true
}

dependencies {
    implementation Libs.kotlin_stdlib
    implementation Libs.kotlin_coroutines

    implementation Libs.libs_support

    implementation Libs.firebase_core
    implementation Libs.firebase_config
    freeImplementation Libs.firebase_ads

    implementation Libs.material_chooser
    implementation Libs.license_dialog
    implementation Libs.keyboard_visibility
    implementation Libs.support_preference_fix
    freeImplementation(Libs.fabric_crashlytics) {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
