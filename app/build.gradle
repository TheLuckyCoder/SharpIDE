apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

ext {
    versionMajor = 1
    versionMinor = 6
    versionPatch = 8
    versionBuild = 4
}

android {
    compileSdkVersion Versions.compile_sdk
    buildToolsVersion Versions.build_tools

    defaultConfig {
        applicationId "net.theluckycoder.scriptcraft"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionName "$versionMajor.$versionMinor.$versionPatch.$versionBuild"
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        resConfigs "en", "fr", "ro", "ru"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            resValue "bool", "debug_build", "true"
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
        }
        release {
            resValue "bool", "debug_build", "false"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "sdk"
    productFlavors {
        fastBuild {
            dimension "sdk"
            versionNameSuffix "-fast"

            // Enable pre-dexing to produce an APK that can be tested on
            // Android 5.0+ without the time-consuming DEX build processes.
            minSdkVersion 21
        }

        regular {
            dimension "sdk"
        }
    }

    lintOptions {
        abortOnError false
    }
}

kotlin.experimental.coroutines "enable"

ktlint {
    version = "0.23.1"
    verbose = true
    android = true
    ignoreFailures = true
}

dependencies {
    implementation Libs.kotlin_stdlib
    implementation Libs.kotlin_coroutines
    implementation Libs.kotlin_anko

    implementation Libs.libs_support

    implementation Libs.firebase_core
    implementation Libs.firebase_config
    implementation Libs.firebase_ads

    implementation Libs.material_chooser
    implementation Libs.license_dialog
    implementation Libs.keyboard_visibility
    implementation Libs.support_preference_fix
    implementation(Libs.fabric_crashlytics) {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
