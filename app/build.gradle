apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.firebase.crashlytics'

ext {
    versionMajor = 1
    versionMinor = 8
    versionPatch = 0
    versionBuild = 0
}

android {
    compileSdkVersion Versions.compile_sdk

    defaultConfig {
        applicationId "net.theluckycoder.scriptcraft"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionName "$versionMajor.$versionMinor.$versionPatch.$versionBuild"
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
        resConfigs "en", "fr", "ro", "ru", "ja", "ko"
        vectorDrawables.useSupportLibrary = true

        resValue "string", "app_name", "SharpIDE"
        resValue "string", "banner_ad_unit_id", "ca-app-pub-1279472163660969/8935487535"
    }

    viewBinding {
        enabled = true
    }

    buildTypes {
        debug {
            manifestPlaceholders = [firebaseDisabled: true, crashlyticsEnabled: false]

            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            aaptOptions.cruncherEnabled = false
            versionNameSuffix "-debug"
            multiDexEnabled true
        }
        release {
            manifestPlaceholders = [firebaseDisabled: false, crashlyticsEnabled: true]

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            shrinkResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation Libs.kotlin_stdlib
    implementation Libs.kotlin_coroutines

    implementation Libs.androidx_appcompat
    implementation Libs.androidx_material
    implementation Libs.androidx_preference

    implementation Libs.ktx_core

    implementation Libs.firebase_core
    implementation Libs.firebase_config
    implementation Libs.firebase_ads
    implementation Libs.firebase_crashlytics

    implementation Libs.material_chooser
    implementation Libs.license_dialog
    implementation Libs.keyboard_visibility
}

apply plugin: 'com.google.gms.google-services'
